# Original Author was Kalle@kde.org
# I lifted it in some mater. (Stephan Kulow)
# I used much code from Janos Farkas

dnl Process this file with autoconf to produce a configure script.

AC_INIT(acinclude.m4) dnl a source file from your sub dir

dnl This is so we can use kde-common
AC_CONFIG_AUX_DIR(admin)

AC_CANONICAL_SYSTEM
AC_ARG_PROGRAM

dnl Automake doc recommends to do this only here. (Janos)
AM_INIT_AUTOMAKE(konqueror-embedded, 0.2) dnl searches for some needed programs

KDE_SET_PREFIX

dnl generate the config header
AM_CONFIG_HEADER(config.h) dnl at the distribution this done

dnl Checks for programs.
AC_CHECK_COMPILERS
dnl AC_REQUIRE([AC_PROG_CPP])
AC_ENABLE_SHARED(yes)
AC_ENABLE_STATIC(no)
KDE_PROG_LIBTOOL

KDE_CHECK_EXTRA_LIBS
AC_FIND_JPEG
AM_CONDITIONAL(HAVE_LIBJPEG, test -n "$LIBJPEG" )

dnl for NLS support. Call them in this order!
dnl WITH_NLS is for the po files
AM_KDE_WITH_NLS

UIC_NOT_NEEDED=true

dnl QT/Embedded support
AC_ARG_ENABLE(qt-embedded,[  --enable-qt-embedded    Enable Qt Embedded support (adds -fno-rtti and -DQWS)],
            want_qte=$enableval,want_qte=no)

KDE_USE_QT(3.1)

AC_BASE_PATH_KDE([don't test]) dnl do not require kdelibs

AC_CREATE_KFSSTND(default)
AC_SUBST_KFSSTND

KDE_CREATE_LIBS_ALIASES

AC_ARG_ENABLE(qpe,[  --enable-qpe            Enable support for the Qt Palmtop Environment],
              want_qpe=$enableval,want_qpe=no)


if test "x$want_qpe" = "xyes"; then
    AC_PATH_QTOPIA(1.5.0)
    dnl hack
    CXXFLAGS="$CXXFLAGS -D_QT_QPE_"
fi

dnl Checks for header files.
AC_HEADER_DIRENT
AC_HEADER_STDC
AC_CHECK_HEADERS(sys/time.h sys/param.h \
        sys/select.h sys/ioctl.h sys/stropts.h stropts.h \
        sys/sockio.h paths.h strings.h string.h sys/stat.h )


dnl Checks for typedefs, structures, and compiler characteristics.
AC_LANG_CPLUSPLUS
AC_HEADER_TIME

dnl check if the compiler has bool
AC_CHECK_BOOL
AC_LANG_C
AC_C_LONG_DOUBLE
AC_TYPE_GETGROUPS

dnl for libsmb
AC_CHECK_SIZEOF(char,1)
AC_CHECK_SIZEOF(short,2)
AC_CHECK_SIZEOF(int,4)
AC_CHECK_SIZEOF(long,4)
AC_C_BIGENDIAN

dnl check if the compiler has bool
AC_CHECK_BOOL

dnl Checks for library functions. 
AC_CHECK_FUNCS(socket snprintf vsnprintf)
AC_CHECK_GETDOMAINNAME
AC_CHECK_GETHOSTNAME
AC_CHECK_USLEEP
AC_CHECK_SETENV
AC_CHECK_UNSETENV
AC_CHECK_RANDOM
AC_CHECK_MKSTEMPS
AC_CHECK_S_ISSOCK
AC_CHECK_KDEMAXPATHLEN
AC_CHECK_SOCKLEN_T
AC_CHECK_INITGROUPS

# configure would do this very late. Too late for us!
test "x$prefix" = xNONE && prefix=$ac_default_prefix

AC_DEFINE_UNQUOTED(KDEDIR, "$prefix", [The prefix to use as fallback])
KDE_EXPAND_MAKEVAR(ac_kde_sysconfdir, sysconfdir)
AC_DEFINE_UNQUOTED(KDESYSCONFDIR, "$ac_kde_sysconfdir", [The compiled in system configuration prefix])

dnl CXXFLAGS="$CXXFLAGS -DQT_CLEAN_NAMESPACE -DQT_NO_COMPAT -DQT_NO_ASCII_CAST"
CXXFLAGS="$CXXFLAGS -DQT_NO_ASCII_CAST"

KDE_CHECK_BINUTILS

KDE_INIT_DOXYGEN([The KDE API Reference], [Version $VERSION])
